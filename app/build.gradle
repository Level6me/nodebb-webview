// Main build script for app
// 
// Usage: gradlew assembleDebug           Build debug APK
//        gradlew assembleRelease         Build production APK
//        gradle wrapper                  Create Gradle Wrapper


buildscript {
    repositories {
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin'}
    }
    dependencies {
    }
}
repositories {
    maven { url 'https://maven.aliyun.com/nexus/content/repositories/google' }
}

final VERSION_MAJOR = 1 // Max 2 digits
final VERSION_MINOR = 0 // Max 2 digits
final VERSION_PATCH = 2 // Max 2 digits
final VERSION_BUILD = 1 // Max 3 digits

apply plugin: 'com.android.application'

dependencies
{
	implementation 'com.xsir:PgyerAndroidAppUpdate:0.8'
	implementation 'com.android.support:support-v4:27.1.1'
	implementation 'com.android.support:appcompat-v7:27.1.1'
	implementation 'commons-io:commons-io:2.6'
}


android
{
	compileSdkVersion 27

	defaultConfig
	{
		multiDexEnabled true
		minSdkVersion 19
		targetSdkVersion 27
		versionCode VERSION_MAJOR*10000000 + VERSION_MINOR*100000 + VERSION_PATCH*1000 + VERSION_BUILD
		versionName "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}"
	}

	lintOptions {
		checkReleaseBuilds false
	}

	buildTypes
	{
		debug
		{
			versionNameSuffix "-debug"
		}

		release
		{
			zipAlignEnabled true
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	applicationVariants.all { variant ->
		variant.outputs.all { output ->
			def newApkName = applicationId + "-" + variant.versionName + "(" + variant.versionCode + ")" + ".apk";
			outputFileName = new File(variant.name, newApkName);
		}
	}
}
